%  Script Mission - Single Lunar Swingby Example
%
%  This script demonstrates how to set up a single Lunar swingby with
%  multiple targeting sequences.
%



%**************************************************************************
%************ Create Objects for Use in Mission Sequence ******************
%**************************************************************************


%--------------------------------------------------------------------------
%----------------- SpaceCraft, Formations, Constellations -----------------
%--------------------------------------------------------------------------


Create Spacecraft MMSRef;
GMAT MMSRef.Epoch.UTCGregorian = 22 Jul 2014 11:29:10.811;
GMAT MMSRef.CoordinateSystem = EarthMJ2000Eq;
GMAT MMSRef.DisplayStateType = Cartesian;
GMAT MMSRef.AnomalyType = TA;
GMAT MMSRef.X = -137380.19843385062;
GMAT MMSRef.Y = 75679.878675370544;
GMAT MMSRef.Z = 21487.638751878556;
GMAT MMSRef.VX = -0.23245320142355028;
GMAT MMSRef.VY = -0.44627539677580192;
GMAT MMSRef.VZ = 0.085612056628771024;
GMAT MMSRef.DryMass = 1000;
GMAT MMSRef.Cd = 2.2000000000000002;
GMAT MMSRef.Cr = 1.7;
GMAT MMSRef.DragArea = 15;
GMAT MMSRef.SRPArea = 1;

%--------------------------------------------------------------------------
%--------------------------------- Burns ----------------------------------
%--------------------------------------------------------------------------

Create ImpulsiveBurn LSI;
GMAT LSI.Axes = VNB;
GMAT LSI.Element1 = 0.14676929889000001;
GMAT LSI.Element2 = 0.046042675892;
GMAT LSI.Element3 = 0.090223244096999999;


Create ImpulsiveBurn ALM;
GMAT ALM.Axes = VNB;
GMAT ALM.Element1 = -0.3198120104;

%--------------------------------------------------------------------------
%------------------------------ Propagators -------------------------------
%--------------------------------------------------------------------------

Create ForceModel LunarSB_ForceModel;
GMAT LunarSB_ForceModel.CentralBody = Earth;
GMAT LunarSB_ForceModel.PointMasses = {Earth, Sun, Luna};
GMAT LunarSB_ForceModel.Drag = None;
GMAT LunarSB_ForceModel.SRP = Off;
GMAT LunarSB_ForceModel.ErrorControl = RSSStep;

Create Propagator LunarSB;
GMAT LunarSB.FM = LunarSB_ForceModel;
GMAT LunarSB.Type = RungeKutta89;
GMAT LunarSB.InitialStepSize = 60;
GMAT LunarSB.Accuracy = 1e-011;
GMAT LunarSB.MinStep = 0.001;
GMAT LunarSB.MaxStep = 5000;
GMAT LunarSB.MaxStepAttempts = 50;

Create ForceModel MoonCentered_ForceModel;
GMAT MoonCentered_ForceModel.CentralBody = Luna;
GMAT MoonCentered_ForceModel.PointMasses = {Earth, Sun, Luna};
GMAT MoonCentered_ForceModel.Drag = None;
GMAT MoonCentered_ForceModel.SRP = Off;
GMAT MoonCentered_ForceModel.ErrorControl = RSSStep;

Create Propagator MoonCentered;
GMAT MoonCentered.FM = MoonCentered_ForceModel;
GMAT MoonCentered.Type = RungeKutta89;
GMAT MoonCentered.InitialStepSize = 60;
GMAT MoonCentered.Accuracy = 1e-011;
GMAT MoonCentered.MinStep = 0.001;
GMAT MoonCentered.MaxStep = 45000;
GMAT MoonCentered.MaxStepAttempts = 50;

%--------------------------------------------------------------------------
%-------------------------------- Solvers ---------------------------------
%--------------------------------------------------------------------------

Create DifferentialCorrector DC1;


%--------------------------------------------------------------------------
%-------------------------- Plots and Reports -----------------------------
%--------------------------------------------------------------------------

Create OpenGLPlot OGL_EarthMJ2K;
GMAT OGL_EarthMJ2K.Add = {MMSRef, Earth, Luna};
GMAT OGL_EarthMJ2K.CoordinateSystem = EarthMJ2000Eq;
GMAT OGL_EarthMJ2K.ViewPointReference = Earth;
GMAT OGL_EarthMJ2K.ViewDirection = Earth;
GMAT OGL_EarthMJ2K.ViewScaleFactor = 40;
GMAT OGL_EarthMJ2K.FixedFovAngle = 38;
GMAT OGL_EarthMJ2K.ViewUpCoordinateSystem = EarthMJ2000Eq;
GMAT OGL_EarthMJ2K.ViewUpAxis = X;
GMAT OGL_EarthMJ2K.CelestialPlane = Off;
GMAT OGL_EarthMJ2K.XYPlane = On;
GMAT OGL_EarthMJ2K.WireFrame = Off;
GMAT OGL_EarthMJ2K.SolverIterations = All;
GMAT OGL_EarthMJ2K.Axes = On;
GMAT OGL_EarthMJ2K.PerspectiveMode = Off;
GMAT OGL_EarthMJ2K.UseFixedFov = Off;
GMAT OGL_EarthMJ2K.DataCollectFrequency = 1;
GMAT OGL_EarthMJ2K.UpdatePlotFrequency = 50;
GMAT OGL_EarthMJ2K.NumPointsToRedraw = 0;

Create OpenGLPlot OGL_MoonMJ2K;
GMAT OGL_MoonMJ2K.Add = {MMSRef, Earth, Luna};
GMAT OGL_MoonMJ2K.CoordinateSystem = MoonMJ2000Eq;
GMAT OGL_MoonMJ2K.ViewPointReference = Luna;
GMAT OGL_MoonMJ2K.ViewDirection = Luna;
GMAT OGL_MoonMJ2K.ViewScaleFactor = 35;
GMAT OGL_MoonMJ2K.FixedFovAngle = 45;
GMAT OGL_MoonMJ2K.ViewUpCoordinateSystem = MoonMJ2000Eq;
GMAT OGL_MoonMJ2K.ViewUpAxis = X;
GMAT OGL_MoonMJ2K.CelestialPlane = Off;
GMAT OGL_MoonMJ2K.XYPlane = On;
GMAT OGL_MoonMJ2K.WireFrame = Off;
GMAT OGL_MoonMJ2K.SolverIterations = All;
GMAT OGL_MoonMJ2K.Axes = On;
GMAT OGL_MoonMJ2K.SunLine = Off;
GMAT OGL_MoonMJ2K.PerspectiveMode = Off;
GMAT OGL_MoonMJ2K.UseFixedFov = Off;
GMAT OGL_MoonMJ2K.DataCollectFrequency = 1;
GMAT OGL_MoonMJ2K.UpdatePlotFrequency = 50;
GMAT OGL_MoonMJ2K.NumPointsToRedraw = 0;

Create OpenGLPlot OGL_EarthMoonRot;
GMAT OGL_EarthMoonRot.Add = {MMSRef, Earth, Luna};
GMAT OGL_EarthMoonRot.CoordinateSystem = EarthMoonRot;
GMAT OGL_EarthMoonRot.ViewPointReference = Luna;
GMAT OGL_EarthMoonRot.ViewDirection = Luna;
GMAT OGL_EarthMoonRot.ViewScaleFactor = 10;
GMAT OGL_EarthMoonRot.FixedFovAngle = 5;
GMAT OGL_EarthMoonRot.ViewUpCoordinateSystem = EarthMoonRot;
GMAT OGL_EarthMoonRot.ViewUpAxis = X;
GMAT OGL_EarthMoonRot.CelestialPlane = Off;
GMAT OGL_EarthMoonRot.XYPlane = On;
GMAT OGL_EarthMoonRot.WireFrame = Off;
GMAT OGL_EarthMoonRot.SolverIterations = All;
GMAT OGL_EarthMoonRot.Axes = On;
GMAT OGL_EarthMoonRot.SunLine = Off;
GMAT OGL_EarthMoonRot.PerspectiveMode = Off;
GMAT OGL_EarthMoonRot.UseFixedFov = Off;
GMAT OGL_EarthMoonRot.DataCollectFrequency = 1;
GMAT OGL_EarthMoonRot.UpdatePlotFrequency = 50;
GMAT OGL_EarthMoonRot.NumPointsToRedraw = 0;

Create XYPlot RadApoPlot
GMAT RadApoPlot.IndVar = MMSRef.CurrA1MJD;
GMAT RadApoPlot.Add    = { MMSRef.RMAG };
GMAT RadApoPlot.Grid = On;
GMAT RadApoPlot.SolverIterations = None;

Create ReportFile Data;
GMAT Data.Filename = Ex_MMSSingleLunarSwingby.report;
GMAT Data.Precision = 16;
GMAT Data.WriteHeaders = Off;
GMAT Data.ColumnWidth = 20;

%--------------------------------------------------------------------------
%-------------------------- Coordinate Systems ----------------------------
%--------------------------------------------------------------------------

Create CoordinateSystem MoonMJ2000Eq;
GMAT MoonMJ2000Eq.Origin = Luna;
GMAT MoonMJ2000Eq.Axes = MJ2000Eq;

Create CoordinateSystem EarthMoonRot;
GMAT EarthMoonRot.Origin = Luna;
GMAT EarthMoonRot.Axes = ObjectReferenced;
GMAT EarthMoonRot.XAxis = R;
GMAT EarthMoonRot.ZAxis = N;
GMAT EarthMoonRot.Primary = Earth;
GMAT EarthMoonRot.Secondary = Luna;

Create CoordinateSystem EarthSunRot;
GMAT EarthSunRot.Origin = Earth;
GMAT EarthSunRot.J2000Body = Earth;
GMAT EarthSunRot.Axes = ObjectReferenced;
GMAT EarthSunRot.Epoch = 21545.000000397937;
GMAT EarthSunRot.UpdateInterval = 60;
GMAT EarthSunRot.XAxis = R;
GMAT EarthSunRot.ZAxis = N;
GMAT EarthSunRot.Primary = Sun;
GMAT EarthSunRot.Secondary = Earth;

%**************************************************************************
%**************************The Mission Sequence****************************
%**************************************************************************
BeginMissionSequence
Toggle RadApoPlot Off;

%------------------------------
%  Propagate to Earth periapsis
%------------------------------
Propagate LunarSB(MMSRef, {MMSRef.Periapsis});

%------------------------------
%  Target Lunar B-plane
%------------------------------
Target DC1

     Vary DC1(LSI.Element1 = 0.1462 ,  {Perturbation = 1e-005, MaxStep = .01, Lower = .13, Upper = .5});
     %Vary DC1(LSI.Element2 = 0.046042  ,       {Pert = 1e-005, MaxStep = .01, Lower = -.5, Upper = .5});
     Vary DC1(LSI.Element3 = 0.1086 , {Perturbation = 1e-005, MaxStep = .01, Lower = -.5, Upper = .5});

     Maneuver LSI(MMSRef);
     
     Propagate LunarSB(MMSRef, {MMSRef.ElapsedDays = 1.5});
     Propagate MoonCentered(MMSRef, {MMSRef.Luna.Periapsis});

     Achieve DC1(MMSRef.MoonMJ2000Eq.BdotT = 15000.4401777 , {Tolerance = 3}); 
     Achieve DC1(MMSRef.MoonMJ2000Eq.BdotR = 4000.59308992  , {Tolerance = 3}); 
     %Achieve DC1(MMSRef.RMAG = 390000 , {Tolerance = 200000}); 

EndTarget
Report Data MMSRef.TAIModJulian MMSRef.Earth.RadPer  MMSRef.Earth.RadApo  MMSRef.EarthMJ2000Eq.INC  MMSRef.EarthMJ2000Eq.RAAN  MMSRef.EarthMJ2000Eq.AOP  MMSRef.Earth.TA  MMSRef.Earth.VelPeriapsis 

%------------------------------
% Propagate to Earth Periapsis
%------------------------------
Propagate LunarSB(MMSRef, {MMSRef.Periapsis});
Report Data MMSRef.TAIModJulian MMSRef.Earth.RadPer  MMSRef.Earth.RadApo  MMSRef.EarthMJ2000Eq.INC  MMSRef.EarthMJ2000Eq.RAAN  MMSRef.EarthMJ2000Eq.AOP  MMSRef.Earth.TA  MMSRef.Earth.VelPeriapsis 

%------------------------------
% Target to lower Apogee
%------------------------------
Target DC1

     Vary DC1(ALM.Element1 = -0.3198120104, {Perturbation = 1e-006, MaxStep = .08, Lower = -.5, Upper = .5});

     Maneuver ALM(MMSRef);

     Achieve DC1(MMSRef.Earth.RadApo = 191340, {Tolerance = 0.1}); 

EndTarget
Report Data MMSRef.TAIModJulian MMSRef.Earth.RadPer  MMSRef.Earth.RadApo  MMSRef.EarthMJ2000Eq.INC  MMSRef.EarthMJ2000Eq.RAAN  MMSRef.EarthMJ2000Eq.AOP  MMSRef.Earth.TA  MMSRef.Earth.VelPeriapsis 


%------------------------------
% Propagate for a few days
%------------------------------
Toggle RadApoPlot On;
Propagate LunarSB(MMSRef, {MMSRef.ElapsedDays = 12});
Report Data MMSRef.TAIModJulian MMSRef.Earth.RadPer  MMSRef.Earth.RadApo  MMSRef.EarthMJ2000Eq.INC  MMSRef.EarthMJ2000Eq.RAAN  MMSRef.EarthMJ2000Eq.AOP  MMSRef.Earth.TA  MMSRef.Earth.VelPeriapsis 
