%  Script Mission - Libration Point Example

% -------------------------------------------------------------------------
% --------------------------- Create Objects ------------------------------
% -------------------------------------------------------------------------


% ----------------------Spacecraft/Propagators-----------------------------

Create Spacecraft Sat
Sat.DateFormat = TAIModJulian
Sat.Epoch = 25219.50041705303
Sat.DisplayStateType = Cartesian
Sat.X = 406326.22661319101
Sat.Y = 177458.38761597816
Sat.Z = 145838.58079004413
Sat.VX = -0.51727467382287506
Sat.VY = 0.77465036656089314
Sat.VZ = 0.33141660265398565

Create ForceModel EarthMoonL2Prop_ForceModel
EarthMoonL2Prop_ForceModel.CentralBody = Earth
EarthMoonL2Prop_ForceModel.PointMasses = {Earth, Luna, Sun}

Create Propagator EarthMoonL2Prop
EarthMoonL2Prop.FM = EarthMoonL2Prop_ForceModel
EarthMoonL2Prop.Type = RungeKutta68
EarthMoonL2Prop.InitialStepSize = 130
EarthMoonL2Prop.Accuracy = 1e-010
EarthMoonL2Prop.MinStep = 0.001
EarthMoonL2Prop.MaxStep = 24000

Create ForceModel EarthProp_ForceModel
EarthProp_ForceModel.CentralBody = Earth
EarthProp_ForceModel.PointMasses = {Sun, Earth, Luna}

Create Propagator EarthProp
EarthProp.FM = EarthProp_ForceModel
EarthProp.Type = PrinceDormand78
EarthProp.InitialStepSize = 60
EarthProp.Accuracy = 1e-011
EarthProp.MinStep = 0.001
EarthProp.MaxStep = 2700

Create ImpulsiveBurn dV
dV.Axes = VNB
dV.Origin = Earth
dV.Element1 = -0.00003

Create ImpulsiveBurn dVCirc
dVCirc.Axes = VNB
dVCirc.Origin = Earth
dVCirc.Element1 = -0.00003

Create DifferentialCorrector DC

Create LibrationPoint EarthMoonL2
EarthMoonL2.Primary = Earth
EarthMoonL2.Secondary = Luna
EarthMoonL2.Point = L2

Create Variable I J

% ------------------------------Coordinate Systems-------------------------

Create CoordinateSystem CSEarthMoonRot
CSEarthMoonRot.Origin = Earth
CSEarthMoonRot.Axes = ObjectReferenced
CSEarthMoonRot.XAxis = R
CSEarthMoonRot.ZAxis = N
CSEarthMoonRot.Primary = Earth
CSEarthMoonRot.Secondary = Luna

Create CoordinateSystem L2Centered
L2Centered.Origin = EarthMoonL2
L2Centered.Axes = ObjectReferenced
L2Centered.XAxis = R
L2Centered.ZAxis = N
L2Centered.Primary = Earth
L2Centered.Secondary = Luna

% ------------------------------OrbitView Plots--------------------------------------

Create OrbitView ViewEarthMoonRot
ViewEarthMoonRot.Add = {Earth, Luna, Sun, Sat, EarthMoonL2}
ViewEarthMoonRot.CoordinateSystem = L2Centered
ViewEarthMoonRot.ViewPointReference = EarthMoonL2
ViewEarthMoonRot.ViewDirection = EarthMoonL2
ViewEarthMoonRot.ViewScaleFactor = 10
ViewEarthMoonRot.CelestialPlane = Off
ViewEarthMoonRot.Axes = On
ViewEarthMoonRot.XYPlane = On

Create OrbitView EarthMJ2K
EarthMJ2K.Add = {Sat, Earth, Luna}
EarthMJ2K.CoordinateSystem = EarthMJ2000Eq
EarthMJ2K.ViewPointReference = Earth
EarthMJ2K.ViewDirection = Earth
EarthMJ2K.ViewScaleFactor = 45
EarthMJ2K.ViewUpCoordinateSystem = EarthMJ2000Eq
EarthMJ2K.ViewUpAxis = X
EarthMJ2K.CelestialPlane = Off
EarthMJ2K.XYPlane = On
EarthMJ2K.Axes = On
EarthMJ2K.UseInitialView = On

Create OrbitView L2View
L2View.Add = {Sat, Earth, EarthMoonL2, Luna}
L2View.CoordinateSystem = L2Centered
L2View.ViewPointReference = EarthMoonL2
L2View.ViewDirection = EarthMoonL2
L2View.ViewScaleFactor = 5
L2View.CelestialPlane = Off
L2View.XYPlane = Off
L2View.Axes = On

% ------------------------------XYPlots--------------------------------------

Create XYPlot ERRot_YZ
GMAT ERRot_YZ.XVariable = Sat.CSEarthMoonRot.Y
GMAT ERRot_YZ.YVariables = { Sat.CSEarthMoonRot.Z}
GMAT ERRot_YZ.ShowGrid = true
GMAT ERRot_YZ.SolverIterations = None

% ------------------------------ Reports --------------------------------------

Create ReportFile Data
Data.WriteHeaders = Off
GMAT Data.Filename = Ex_L2Design.report

% -------------------------------------------------------------------------
% --------------------------- Mission Sequence-----------------------------
% -------------------------------------------------------------------------
BeginMissionSequence

For I = 1:1
       Propagate EarthMoonL2Prop(Sat, {Sat.CSEarthMoonRot.Y = 0})
EndFor
Report Data Sat.TAIModJulian Sat.L2Centered.X Sat.L2Centered.Y Sat.L2Centered.Z Sat.L2Centered.VX Sat.L2Centered.VY Sat.L2Centered.VZ

For J = 1:2

   Target DC
      
     PenDown ERRot_YZ
     Vary DC(dV.Element1 =  0.00005, {Perturbation = 1e-6, MaxStep = 0.0005, Lower = -0.04 , Upper =  0.0297})
     Maneuver dV(Sat)
     
     For I = 1:2
       Propagate EarthMoonL2Prop(Sat, {Sat.L2Centered.Y = 0, StopTolerance = 1e-5})
     EndFor
     
     Achieve DC(Sat.L2Centered.VX = 0.00125, {Tolerance = 0.00002})
     PenUp ERRot_YZ

   EndTarget
   Report Data Sat.TAIModJulian Sat.L2Centered.X Sat.L2Centered.Y Sat.L2Centered.Z Sat.L2Centered.VX Sat.L2Centered.VY Sat.L2Centered.VZ

EndFor
